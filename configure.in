dnl Process this file with autoconf to produce a configure script.
AC_INIT(quota.c)

dnl Checks for programs.
AC_PROG_CC
AC_HEADER_STDC
AC_PROG_INSTALL

dnl Checks for libraries.
AC_ARG_WITH(ext2direct,
	[  --with-ext2direct=[yes/no/try]     Enable alternative format used by edquota [default=try].],
	,
	with_ext2direct="try")
if test "x$with_ext2direct" != "xno"; then
	AC_CHECK_LIB(com_err, com_err, EXT2LIBS="-lcom_err $EXT2LIBS")
	AC_CHECK_LIB(ext2fs, ext2fs_initialize, EXT2LIBS="-lext2fs $EXT2LIBS",, "-lcom_err")
	if test "${ac_cv_lib_com_err_com_err}" != "yes" -o "${ac_cv_lib_ext2fs_ext2fs_initialize}" != "yes"; then
		if test "x$with_ext2direct" = "xyes"; then
			AC_MSG_ERROR([Ext2 direct support required but libraries not found.])
		else
			AC_MSG_WARN([Ext2 direct support won't be compiled. Libraries not found.])
		fi
		with_ext2direct="no"
		EXT2LIBS=""
	else
		AC_CHECK_HEADER(ext2fs/ext2fs.h,ext2fshead="yes")
		if test "x$ext2fshead" != "xyes"; then
			if test "x$with_ext2direct" = "xyes"; then
				AC_MSG_ERROR([Ext2 direct support required but header files not found.])
			else
				AC_MSG_WARN([Ext2 direct support won't be compiled. Header files not found.])
			fi
			with_ext2direct="no"
			EXT2LIBS=""
		else
			with_ext2direct="yes"
			AC_MSG_CHECKING([for ext2_ino_t])
			have_ext2_ino_t="no"
			AC_EGREP_HEADER(ext2_ino_t, ext2fs/ext2fs.h, have_ext2_ino_t="yes")
			if test "x$have_ext2_ino_t" != "xyes"; then
				AC_MSG_RESULT([not found])
			else
				AC_MSG_RESULT([found])
				CFLAGS="-DHAVE_EXT2_INO_T $CFLAGS"
			fi
		fi
	fi
	if test "x$with_ext2direct" = "xyes"; then
		EXT2_DIRECT="-DEXT2_DIRECT"
		AC_SUBST(EXT2_DIRECT)
	fi
fi
AC_SUBST(EXT2LIBS)

AC_CHECK_LIB(nsl, main)
AC_CACHE_VAL(ac_cv_lib_wrap_main,
	saved_LIBS="$LIBS"
	LIBS="$LIBS -lwrap"
	[AC_TRY_LINK([#include <stdio.h>
	#include <sys/types.h>
	#include <sys/socket.h>
	#include <netinet/in.h>
	#include <tcpd.h>
	struct request_info request;
	int deny_severity, allow_severity;],[hosts_access(&request);],
	[ac_cv_lib_wrap_main=yes],[ac_cv_lib_wrap_main=no;LIBS="$saved_LIBS"])
	])

if test ${ac_cv_lib_wrap_main} = yes; then
	AC_CHECK_HEADER(tcpd.h,, [
		echo 'ERROR: could not find tcpd.h - missing TCP wrappers package'
		exit 1
	])
	dnl Do this in case value was cached...
	LIBS="$LIBS -lwrap"
	HOSTS_ACCESS="-DHOSTS_ACCESS"
	AC_SUBST(HOSTS_ACCESS)
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE

AC_ARG_ENABLE(altformat,
	[  --enable-altformat=[yes/no]     Enable alternative format used by edquota [default=yes].],
	,
	enable_altformat="yes")
AC_ARG_ENABLE(rpc,
	[  --enable-rpc=[yes/no]           Enable RPC support [default=yes].],
	,
	enable_rpc="yes")
AC_ARG_ENABLE(rpcsetquota,
	[  --enable-rpcsetquota=[yes/no]   Use RPC for setting quotas [default=no].],
	,
	enable_rpcsetquota="no")
AC_ARG_ENABLE(bsd_behaviour,
	[  --enable-bsd_behaviour=[yes/no]   Mimic BSD behaviour [default=yes].],
	,
	enable_bsd_behaviour="yes")
AC_ARG_ENABLE(libefence,
	[  --enable-libefence=[yes/no]     Use Electric Fence memory checks [default=no].],
	,
	enable_libefence="no")

if test "$enable_altformat" = "yes" ; then
	CFLAGS="-DALT_FORMAT $CFLAGS";
fi
if test "$enable_rpc" = "yes" ; then
	CFLAGS="-DRPC $CFLAGS";
fi
if test "$enable_rpcsetquota" = "yes" ; then
	CFLAGS="-DRPC_SETQUOTA $CFLAGS"
fi
if test "$enable_bsd_behaviour" = "yes" ; then
	CFLAGS="-DBSD_BEHAVIOUR $CFLAGS"
fi
if test "$enable_libefence" = "yes" ; then
	LIBMALLOC="/usr/lib/libefence.a"
fi
AC_SUBST(LIBMALLOC)

AC_OUTPUT(Makefile)
